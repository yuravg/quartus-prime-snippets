# -*- mode: snippet -*-
# name: qpp-M18x19_systolic-with-Input-Cascade-Output-Chaining-Accumulator-Double-Accumulator-and-Preload-Constant
# key: qpp-DSP-Features-for-20-nm-Device-M18x19_systolic-with-Input-Cascade-Output-Chaining-Accumulator-Double-Accumulator-and-Preload-Constant
# group: qpp DSP-Features-for-20-nm-Device
# --
// M18x19_systolic with Input Cascade, Output Chaining, Accumulator, Double Accumulator and Preload Constant
// Quartus Prime Verilog Template
// 18x19systolic with full registers (input, pipeline, systolic and output) + input cascade chain (and input delay register) + chainout adder + accumulate + double accumulate + preload constant
// Formula: final_output[t] = acc_sel + a1[t-8]*b1[t-8] + a2[t-7]*b1[t-9] + a3[t-6]*b1[t10] + a4[t-5]*b1[t-11] + a5[t-4]*b1[t-12] + zero_bit_a*zero_bit_b
//          where zero_bit_a*zero_bit_b is a dummy multiplier
//          where acc_sel = final_output[t-2] or preload_value or 0 depending on dynamic control inputs "accum" and "loadconst"
//		Note: Systolic mode do not support dynamic negate and subtraction(sub)
// For use with 20-nm device families

module m18x19systolic_full_regs_inputcascade_chainoutadder_acc_doubleacc_preloadConst (
	a1, a2, a3, a4, a5, b1,
	clock1, clock2, clock3, ena1, ena2, ena3, reset1, reset2,
	accum, loadconst,
	final_output
);

	// This template will only work within the AxB data width range from 2x2 to 18x19.
	parameter A_WIDTH = 18;
	parameter B_WIDTH = 19;

	// PRELOAD_VALUE can be 2 power of N, which N should less than 64
	// PRELOAD_VALUE should contain only one bit 1
	parameter PRELOAD_VALUE = 'h400;

	// The max chain width for systolic mode is 44.
	parameter CHAIN_WIDTH = 44;

	// Double accumulation enable control parameter
	parameter enable_double_accum = "TRUE";

	// Data input ports
	input signed [A_WIDTH-1:0] a1, a2, a3, a4, a5;
	input signed [B_WIDTH-1:0] b1;

	// Register clock and control signals
	// DSP supports up to 3 clock/ena pairs, and 2 async reset signals
	input clock1, clock2, clock3, ena1, ena2, ena3, reset1, reset2;

	// Dynamic ACCUMULATE and LOADCONST control signals
	input accum, loadconst;

	// Output signal
	// Max output width for chaining is 44
	output signed [CHAIN_WIDTH-1:0] final_output;

	// Multiplier Result
	wire signed [A_WIDTH+B_WIDTH-1:0] m1, m2, m3, m4, m5, m6;

	// Summation result
	reg signed [CHAIN_WIDTH-1:0] s1_output_reg, s2_output_reg, s3_output_reg, s4_output_reg, s5_output_reg;

	// Data Input Register
	reg signed [A_WIDTH-1:0] a1_reg, a2_reg, a3_reg, a4_reg, a5_reg;
	reg signed [B_WIDTH-1:0] b1_reg, b2_reg, b3_reg, b4_reg, b5_reg;

	// Data Input Cascade Delay register
	// There are two input delay registers in one DSP block: one in each of the two multipliers.
	// In 18x19 systolic mode, both delay registers in a DSP block can be used.
	reg signed [B_WIDTH-1:0] b1_delay_reg, b2_delay_reg, b3_delay_reg, b4_delay_reg;

	// Data Input Pipeline Register
	reg signed [A_WIDTH-1:0] a1_pipeline_reg, a2_pipeline_reg, a3_pipeline_reg, a4_pipeline_reg, a5_pipeline_reg;
	reg signed [B_WIDTH-1:0] b1_pipeline_reg, b2_pipeline_reg, b3_pipeline_reg, b4_pipeline_reg, b5_pipeline_reg;

	// LOADCONST Input Register
	reg loadconst_reg;

	// LOADCONST Pipeline Register
	reg loadconst_pipeline_reg;

	// ACCUMULATE Input Register
	reg accum_reg;

	// ACCUMULATE Pipeline Register
	reg accum_pipeline_reg;

	// Accumulate, double acc
	wire signed [CHAIN_WIDTH-1:0] selected_value, select_feedback;
	wire signed [CHAIN_WIDTH-1:0] acc_sel;
	reg signed [CHAIN_WIDTH-1:0] s_reg, s_double;

	// When this template is used, the number of multipliers has to be even
	// A dummy 0x0 multiplier can be created if the number of multipliers is odd, to make up the number to even.
	// The following is required for the dummy multiplier.
	reg signed [A_WIDTH-1:0] zero_bit_a_reg, zero_bit_a_pipeline_reg /* synthesis preserve */;
	reg signed [B_WIDTH-1:0] zero_bit_b_reg, zero_bit_b_pipeline_reg /* synthesis preserve */;
	wire signed zero_bit;
	soft sbz (1'b0, zero_bit);

	// accumulator path
	assign acc_sel = accum_pipeline_reg ? select_feedback : selected_value;
	assign select_feedback = (enable_double_accum == "TRUE")? s_double: s_reg;
	assign selected_value = loadconst_pipeline_reg? PRELOAD_VALUE : 0;

	// Input registers (for DATA and DYNAMIC CONTROL SIGNAL), and delay registers
	// All input and delay registers must use the same reset signal.
	// Each DATA input and delay register may have different pair of clock and ena signal.
	// The DYNAMIC CONTROL SIGNAL input registers(e.g. loadconst and accumulate) can have different clock/ena signal pair than that of the DATA input register.
// But all DYNAMIC CONTROL SIGNAL input registers must share the same clock and ena signal.
	always @(posedge clock1 or posedge reset1)
	if (reset1) begin
		// Input registers (for DATA)
		a1_reg <= 0;
		b1_reg <= 0;
		a2_reg <= 0;
		b2_reg <= 0;
		a3_reg <= 0;
		b3_reg <= 0;
		a4_reg <= 0;
		b4_reg <= 0;
		a5_reg <= 0;
		b5_reg <= 0;
		zero_bit_a_reg <= 0;
		zero_bit_b_reg <= 0;
		// Input registers (for DYNAMIC CONTROL SIGNAL)
		loadconst_reg <= 0;
		accum_reg <= 0;
		// Delay register
		b1_delay_reg <= 0;
		b2_delay_reg <= 0;
		b3_delay_reg <= 0;
		b4_delay_reg <= 0;
	end else begin
		if (ena1) begin
			a1_reg <= a1;
			b1_reg <= b1;
			a2_reg <= a2;
			b2_reg <= b1_delay_reg;
			a3_reg <= a3;
			b3_reg <= b2_delay_reg;
			a4_reg <= a4;
			b4_reg <= b3_delay_reg;
			a5_reg <= a5;
			b5_reg <= b4_delay_reg;
			loadconst_reg <= loadconst;
			accum_reg <= accum;
			b1_delay_reg <= b1_reg;
			b2_delay_reg <= b2_reg;
			b3_delay_reg <= b3_reg;
			b4_delay_reg <= b4_reg;
			// input for dummy multiplier 0x0
			zero_bit_a_reg <= {A_WIDTH{zero_bit}};
			zero_bit_b_reg <= {B_WIDTH{zero_bit}};
		end
	end

	//Input pipeline register (for DATA and DYNAMIC CONTROL SIGNAL)
	// All pipeline registers must use the same {clock, ena, reset}
	// Pipeline register must use the same reset as the output register
// The DYNAMIC CONTROL SIGNAL input pipeline register can be bypass differently
	always @(posedge clock2 or posedge reset2)
	if (reset2) begin
		a1_pipeline_reg <= 0;
		b1_pipeline_reg <= 0;
		a2_pipeline_reg <= 0;
		b2_pipeline_reg <= 0;
		a3_pipeline_reg <= 0;
		b3_pipeline_reg <= 0;
		a4_pipeline_reg <= 0;
		b4_pipeline_reg <= 0;
		a5_pipeline_reg <= 0;
		b5_pipeline_reg <= 0;
		zero_bit_a_pipeline_reg <= 0;
		zero_bit_b_pipeline_reg <= 0;
		loadconst_pipeline_reg <= 0;
		accum_pipeline_reg <= 0;
	end else begin
		if (ena2) begin
			a1_pipeline_reg <= a1_reg;
			b1_pipeline_reg <= b1_reg;
			a2_pipeline_reg <= a2_reg;
			b2_pipeline_reg <= b2_reg;
			a3_pipeline_reg <= a3_reg;
			b3_pipeline_reg <= b3_reg;
			a4_pipeline_reg <= a4_reg;
			b4_pipeline_reg <= b4_reg;
			a5_pipeline_reg <= a5_reg;
			b5_pipeline_reg <= b5_reg;
			zero_bit_a_pipeline_reg <= zero_bit_a_reg;
			zero_bit_b_pipeline_reg <= zero_bit_b_reg;
			loadconst_pipeline_reg <= loadconst_reg;
			accum_pipeline_reg <= accum_reg;
		end
	end

	// Output register
	// Output register must share the same reset with input pipeline register
	// Even though the output registers are not explicitly declared, they will be inferred later during compilation. Thus, it is important to place the s1_output_reg-s5_output_reg operation
	// within the output register enable (i.e. ena3=1) condition.
	always @(posedge clock3 or posedge reset2)
	if (reset2) begin
		s1_output_reg <= 0;
		s2_output_reg <= 0;
		s3_output_reg <= 0;
		s4_output_reg <= 0;
		s5_output_reg <= 0;
		s_reg <= 0;
		s_double <= 0;
	end else begin
		if (ena3) begin
			// chainout adder support static add or sub
			// basic mult result (m) must be the second operand
			s1_output_reg <= m1;
			s2_output_reg <= s1_output_reg + m2;
			s3_output_reg <= s2_output_reg + m3;
			s4_output_reg <= s3_output_reg + m4;
			s5_output_reg <= s4_output_reg + m5;
			// chainout accumulator only support addition when use with chainout adder
			s_reg <= acc_sel + (s5_output_reg + m6); // loopback path (acc_sel) must be the first operand
			// Double Accumulate
			s_double <= s_reg;
		end
	end

	// Multiplier
	assign m1 = (a1_pipeline_reg * b1_pipeline_reg);
	assign m2 = (a2_pipeline_reg * b2_pipeline_reg);
	assign m3 = (a3_pipeline_reg * b3_pipeline_reg);
	assign m4 = (a4_pipeline_reg * b4_pipeline_reg);
	assign m5 = (a5_pipeline_reg * b5_pipeline_reg);
	// When this template is used, the number of multipliers has to be even
// Create a 0x0 multiplier as below to make up for the even number requirement if the number of multipliers is odd
	assign m6 = (zero_bit_a_pipeline_reg * zero_bit_b_pipeline_reg);

	// Final output
assign final_output = s_reg;

endmodule
